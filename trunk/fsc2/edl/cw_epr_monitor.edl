#!/usr/local/bin/fsc2 -T

/***************************************************************************/
/*                                                                         */
/*    EDL program for running cw-EPR test measurements with the 360 GHz    */
/*    spectrometer.                                                        */
/*                                                                         */
/*    $Id$         */
/*                                                                         */
/***************************************************************************/


DEVICES:

sr830;                         // load module for the lock-in
ips20_4;                       // load module for the sweep power supply


VARIABLES:

current;                       // last measured current
sweep_rate;                    // current sweep rate
acq_rate;                      // current acquisition rate

Sweep_State = 0;               // sweep state, 1 = up, -1 = down, 0 = stopped

Cur_Acq_Rate;                  // currently setected acquisition rate menu item

MAX_POINTS = 16350;            // maximum number of data points
data[ MAX_POINTS ];            // array of data points
I;                             // count of data points
File;                          // file handler for storing data

new_current;                   // current as set by user
new_sweep_rate;                // sweep rate as set by user
New_Acq_Rate;                  // user selected acquisition rate menu item
Pause_State = 1;
Old_Pause_State = 1;

/* Variables for all buttons etc. */

Sweep_Up, Sweep_Stop, Sweep_Down, POnS,
Current, Sweep_Rate, Acq_Rate,
Pause, Save, Clear;

/* List of all useful acquisition rates */

acq_rates_list[ 10 ] = { 32 Hz, 16 Hz, 8 Hz, 4 Hz, 2 Hz, 1 Hz, 500 mHz,
						 250 mHz, 125 mHz, 62.5 mHz };

/* Some variables to make the program  easier to read */

STOPPED = 0;
UP = 1;
DOWN = -1;


PREPARATIONS:

/* We need just 1D graphics and 1 curve */

init_1d( 1, "Points", "Signal [mV]" );


EXPERIMENT:

/* We start with asking the magnet for the current and the sweep rate after
   having made sure that the magnet isn't sweeping */

IF magnet_sweep( ) != STOPPED {
	magnet_sweep( STOPPED );
}
current = get_field( );
sweep_rate = magnet_sweep_rate( );
IF sweep_rate < 1.0e-2 A / 60.0 s {
	sweep_rate = 1.0e-2 A / 60.0 s;
}

/* Now ask the lock-in for the current setting of the acquisition rate for
   auto-acquisition, then figure out the index in the array of allowed
   values (which is used later to set up the menu), if necessary reduce
   the maximum acquisition rate to 32 Hz and finally tell the lock-in to
   use this acquisition rate for auto-acqusition of channel 1. */

acq_rate = 1.0 / lockin_get_sample_time( );

IF acq_rate >= acq_rates_list[ 1 ] {
	acq_rate = acq_rates_list[ 1 ];
	Cur_Acq_Rate = 1;
} ELSE {
	FOR I = 2 : size( acq_rates_list )
	{
		IF abs( acq_rates_list[ I ] - acq_rate ) < 0.0001 Hz
		{
			Cur_Acq_Rate = I;
			BREAK;
		}
	}
}

lockin_auto_setup( 1.0 / acq_rates_list[ Cur_Acq_Rate ], 1 );

/* Unlock the lock-ins keyboard */

lockin_lock_keyboard( "OFF" );

/* Finally, create the form with the buttons for sweeping etc. */

hide_toolbox( "ON" );
Sweep_Up   = button_create( "RADIO_BUTTON", "Sweep up" );
Sweep_Stop = button_create( "RADIO_BUTTON", Sweep_Up, "Stop sweep" );
Sweep_Down = button_create( "RADIO_BUTTON", Sweep_Up, "Sweep Down" );
button_state( Sweep_Stop, "ON" );
POnS       = button_create( "PUSH_BUTTON", "Pause On Stop" );
button_state( POnS, "ON" );

Current    = input_create( "FLOAT_INPUT", current, "Set new current (in A)" );
Sweep_Rate = input_create( "FLOAT_INPUT", sweep_rate * 60.0,
						   "Set new sweep rate (in A/min)" );
Acq_Rate   = menu_create( "Lock-in acquisition rate", "32 Hz", "16 Hz", "8 Hz",
						   "4 Hz", "2 Hz", "1 Hz", "1/2 Hz", "1/4 Hz",
						   "1/8 Hz", "1/16 Hz" );
menu_choice( Acq_Rate, Cur_Acq_Rate );
Pause = button_create( "PUSH_BUTTON", "Pause display" );
button_state( Pause, 1 );
Save  = button_create( "NORMAL_BUTTON", "Save data" );
Clear = button_create( "NORMAL_BUTTON", "Clear screen" );
hide_toolbox( "OFF" );


/* Now we're done with all preparations and can start to run the experiment
   within an endless loop */

I = 0;

FOREVER {

	/* Check if the sweep up button has been pressed and we're not already
	   sweeping up - distinguish between the start of a sweep and the reversal
	   of the sweep direction, in the later case also stop and restart the
	   lock-in's auto-acquisition to empty its internal data buffer. */

	IF  Sweep_State != UP & button_state( Sweep_Up ) & I < MAX_POINTS 	{
		IF Sweep_State == STOPPED {
			magnet_sweep( UP );
			Sweep_State = UP;
			lockin_auto_acquisition( "ON" );
		}

		IF Sweep_State == DOWN {
			lockin_auto_acquisition( "OFF" );
			magnet_sweep( UP );
			Sweep_State = UP;
			lockin_auto_acquisition( "ON" );
		}

		draw_marker( I + 1, "YELLOW" );
		button_state( Pause, 0 );
	}

	/* Check if the sweep down button has been pressed and we're not already
	   sweeping down  - distinguish between the start of a sweep and the
	   reversal of the sweep direction, in the later case also stop and restart
	   the lock-in's auto-acquisition to empty its internal data buffer. */

	IF Sweep_State != DOWN & button_state( Sweep_Down ) & I < MAX_POINTS {
		IF Sweep_State == STOPPED {
			magnet_sweep( DOWN );
			Sweep_State = DOWN;
			lockin_auto_acquisition( "ON" );
		}

		IF Sweep_State == UP {
			lockin_auto_acquisition( "OFF" );
			magnet_sweep( DOWN );
			Sweep_State = DOWN;
			lockin_auto_acquisition( "ON" );
		}

		draw_marker( I + 1, "BLUE" );
		button_state( Pause, 0 );
	}

	/* Check if the sweep stop button has been pressed while we're sweeping */

	IF Sweep_State != STOPPED & button_state( Sweep_Stop ) {
		lockin_auto_acquisition( "OFF" );
		magnet_sweep( STOPPED );
		Sweep_State = STOPPED;
		draw_marker( I + 1, "WHITE" );

		IF button_state( POnS ) {
			Pause_State = 1;
			button_state( Pause, 1 );
		} ELSE {
			lockin_auto_acquisition( "ON" );
		}
	}

	/* Check if a new current has been set - if yes go to the new current
	   (which automatically stops a sweep) after checking that it's within
	   the allowed limits */

	new_current = input_value( Current );
	IF abs( current - new_current ) > 0.01 mA {
		IF new_current > 7.0 A | new_current < -7.0 A {
			current = get_field( );
		} ELSE {
			lockin_auto_acquisition( "OFF" );
			button_state( Sweep_Stop, "ON" );
			current = set_field( new_current );
			Sweep_State = STOPPED;
			draw_marker( I + 1, "WHITE" );
		}
		input_value( Current, current );
	}

	/* Check if a new sweep rate has been set - if yes set the new rate after
	   checking that it's within the allowed limits - take care: the user
	   input is in A/min while the magnet driver expects sweep rates in A/s.
	   Because any data stored in the lock-ins internal data buffer have
	   become meaningless stop and restart auto-acquisition to clean out the
	   buffer. */

	new_sweep_rate = abs( input_value( Sweep_Rate ) );
	IF abs( sweep_rate * 60.0 - new_sweep_rate ) > 0.0001 {
		IF new_sweep_rate <= 1.0 & new_sweep_rate >= 0.01 {
			sweep_rate = magnet_sweep_rate( new_sweep_rate / 60.0 );
			IF Sweep_State != STOPPED {
				lockin_auto_acquisition( "OFF" );
				lockin_auto_acquisition( "ON" );
				draw_marker( I + 1, "GREEN" )
			}
		}
		input_value( Sweep_Rate, sweep_rate * 60.0 );
	}

	/* Check if a new acquisition rate for the lock-in has been selected -
	   also in this case the data in the lock-ins buffer aren't useful any-
	   more so clean them by stopping and restarting the auto-acquisition. */

	New_Acq_Rate = menu_choice( Acq_Rate );
	IF New_Acq_Rate != Cur_Acq_Rate {
		Cur_Acq_Rate = New_Acq_Rate;
		acq_rate = acq_rates_list[ Cur_Acq_Rate ];
		lockin_auto_setup( 1.0 / acq_rate, 1 );
		IF Sweep_State != STOPPED {
			lockin_auto_acquisition( "OFF" );
			lockin_auto_acquisition( "ON" );
		}
	}

	/* Check if the Save button has been pressed (but save only as many points
	   has have been measured, not the complete array) */

	IF button_state( Save ) {
		File = get_file( );
		IF is_file( File ) {
			save( File, slice( data, 1, I ) );
		}
	}

	Old_Pause_State = Pause_State;
	Pause_State = button_state( Pause );
	IF Pause_State != Old_Pause_State {
		lockin_auto_acquisition( ! Pause_State );
		Old_Pause_State = Pause_State;
	}

	/* Check if the clear curve button has been pressed - if yes also clear
	   the lock-ins internal buffer. */

	IF button_state( Clear ) {
		clear_curve( );
		clear_marker( );
		rescale( 64 );
		I = 0;
		IF Sweep_State != STOPPED {
			lockin_auto_acquisition( "OFF" );
			lockin_auto_acquisition( "ON" );
		}
	}

	/* If the magnet is sweeping the field get a new data point, otherwise
	   wait a short moment to keep the program from using up the complete
	   computer time. If we already have acquired MAX_POINTS data points or the
	   magnet sweep has been haltet because one of the current limits had been
	   reached also stop the sweep without further user intervention. */

	IF ! Pause_State {
		I += 1;
		data[ I ] = lockin_get_data( 1 );
		display( I, data[ I ] * 1.0e3 );

		IF ( Sweep_State != STOPPED & magnet_sweep( ) == STOPPED ) |
			I >= MAX_POINTS {
			lockin_auto_acquisition( "OFF" );
			magnet_sweep( STOPPED );
			Sweep_State = STOPPED;
			button_state( Sweep_Stop, "ON" );
			draw_marker( I, "WHITE" );
			IF button_state( POnS ) {
				Pause_State = 1;
			} ELSE {
				lockin_auto_acquisition( "ON" );
			}
		}
	} ELSE {
		wait( 0.25 s );
	}
}


ON_STOP:

/* At the and of the experiment make sure the sweep is stopped and the
   lock-ins auto-acquisition gets stopped. */

IF Sweep_State != STOPPED {
	magnet_sweep( STOPPED );
	lockin_auto_acquisition( "OFF" );
}
