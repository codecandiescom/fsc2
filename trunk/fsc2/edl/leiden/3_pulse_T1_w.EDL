/* $Id$ */

=== TRIG_MODE menu [ "EXTERNAL", "INTERNAL" ] [ 1 ] "Trigger mode"
=== REP_TIME float [ 0.001 : ] [ 50 ] "Repetition time" "ms"
=== P1_POS int [ 200 : 167772150 ] [ 200 ] "Position of 1st MW pulse" "ns"
=== P1_LEN int [ 10 : 167772150 ] [ 100 ] "Length of 1st MW pulse" "ns"
=== P2_LEN int [ 10 : 167772150 ] [ 200 ] "Length of 2nd MW pulse" "ns"
=== P3_LEN int [ 10 : 167772150 ] [ 200 ] "Length of 3rd MW pulse" "ns"
=== P1_P2_DIST int [ 100 : 167772150 ] [ 400 ] "Init. P1-P2 distance" "ns"
=== P1_P2_INCR int [ 10 : ] [ 20 ] "P1-P2 dist. increment" "ns"
=== P2_P3_DIST int [ 100 : 167772150 ] [ 400 ] "P2-P3 distance" "ns"
=== DET_OFFSET int [ -167772150 : 167772150 ] [ 0 ] "Detection offset" "ns"
=== CREATE_DEFENSE button [ ON ] "Create defense pulse"
=== PULSE_2_DEFENSE float [ 0 : ] [ 50 ] "End of MW pulse to\nend of defense\npulse distance" "ns"
=== N_AVG menu [ "1", "3", "10", "30", "100", "300", "1000", \
===              "3000", "10000" ] [ 3 ] "Number of averages"
=== FIELD float [ 0 : 48370.8 ] [ 35000 ] "Field" "G"
=== ADC_CHANNELS menu [ "0", "1", "0 and 1" ] "ADC channel(s) to use"
=== SHOW_PREV button [ OFF ] "Show pulse preview"
DEVICES:

ips120_10;
rb8509;
rb_pulser_w;


VARIABLES:

repeat_time   = REP_TIME ms;
p1_pos        = P1_POS ns;
p1_to_p2_dist = P1_P2_DIST ns;
p2_to_p3_dist = P2_P3_DIST ns;
p1_to_p2_incr = P1_P2_INCR ns;
p1_len        = P1_LEN ns;
p2_len        = P2_LEN ns;
p3_len        = P3_LEN ns;
det_offset    = DET_OFFSET ns;
field         = FIELD G;
N_Avg         = N_AVG;

I;
=== if ( ADC_CHANNELS eq "0 and 1" )
data[ 2 ];
=== else
data;
=== endif
File;
B1, B2;


ASSIGNMENTS:

TIMEBASE: 10 ns;
=== if ( TRIG_MODE eq "EXTERNAL" )
TRIGGER_MODE: EXTERNAL;
=== else
TRIGGER_MODE: TRIG_MODE, REPEAT_TIME = repeat_time;
=== endif


PREPARATIONS:

=== if ! CREATE_DEFENSE
pulser_defense_pulse_mode( "OFF" );
=== else
pulser_minimum_defense_distance( PULSE_2_DEFENSE ns );
=== endif

P1:  FUNCTION    = MICROWAVE,
     START       = p1_pos,
     LENGTH      = p1_len;

P2:  FUNCTION    = MICROWAVE,
     START       = P1.START + p1_to_p2_dist + 0.5 * ( P1.LENGTH - p2_len ),
     DELTA_START = p1_to_p2_incr,
     LENGTH      = p2_len;

P3:  FUNCTION    = MICROWAVE,
     START       = P2.START + p2_to_p3_dist + 0.5 * ( P2.LENGTH - p3_len ),
     DELTA_START = p1_to_p2_incr,
     LENGTH      = p3_len;

P4:  FUNCTION    = DETECTION,
     START       = P3.START + det_offset + p2_to_p3_dist + 0.5 * P3.LENGTH,
     DELTA_START = p1_to_p2_incr,
     LENGTH      = 10 ns;

=== if SHOW_PREV
pulser_show_pulses( );
=== endif

=== if ( ADC_CHANNELS eq "0 and 1" )
init_1d( 2, 0, p1_to_p2_dist / 1 ns, p1_to_p2_incr / 1 ns,
         "Pulse separation [ns]", "Echo amplitude [a.u.]" );
=== else
init_1d( 1, 0, p1_to_p2_dist / 1 ns, p1_to_p2_incr / 1 ns,
         "Pulse separation [ns]", "Echo amplitude [a.u.]" );
=== endif


EXPERIMENT:
=== if ( TRIG_MODE eq "EXTERNAL" )
print( "Please make sure the repetition time is set correctly!\n" );
=== endif

magnet_field( field );

File = get_file( );

hide_toolbox( "ON" );
B1 = output_create( "INT_OUTPUT", "Current P1-P2 separation [ns]" );
B2 = output_create( "FLOAT_OUTPUT", "Field [G]", "%.3f" );
hide_toolbox( "OFF" );

output_value( B1, int( p1_to_p2_dist / 1 ns ) );
output_value( B2, field );

I = 1;
pulser_state( "ON" );
daq_gain( 4 );

FOREVER {
    wait( 1.1 * repeat_time * N_Avg );
=== if ( ADC_CHANNELS eq "0 and 1" )
    data[ 1 ] = daq_get_voltage( CH0 );
    data[ 2 ] = daq_get_voltage( CH1 );
    display( I, data[ 1 ], 1, I, data[ 2 ], 2 );
    fsave( File, "#,#,#\n",
           ( p1_to_p2_dist + p1_to_p2_incr * ( I - 1 ) ) / 1 ns,
		   data[ 1 ], data[ 2 ] );
=== else
=== if ( ADC_CHANNELS eq "0" )
    data = daq_get_voltage( CH0 );
=== else
    data = daq_get_voltage( CH1 );
=== endif
    display( I, data );
    fsave( File, "#,#\n",
           ( p1_to_p2_dist + p1_to_p2_incr * ( I - 1 ) ) / 1 ns, data );
=== endif
    pulser_shift( );
    pulser_update( );
    output_value( B1, int( ( p1_to_p2_dist + I * p1_to_p2_incr ) / 1 ns ) );
    I += 1;
}


ON_STOP:

fsave( File,
	   "\n"
       "% Date:                   # #\n"
       "% Script:                 3_pulse_T1 (J-band, Leiden)\n"
       "% Field:                  # G\n"
       "% Repetition time:        # ms\n"
       "% Length of 1st MW pulse: # ns\n"
       "% Length of 2nd MW pulse: # ns\n"
       "% Length of 3rd MW pulse: # ns\n"
       "% Init. P1-P2 separation: # ns\n"
       "% P1-P2 increment:        # ns\n"
       "% P2-P3 separation:       # ns\n"
       "% Number of averages:     >= #\n"
       "% ADC gain:               4\n"
	   "% ADC channel(s):         ADC_CHANNELS\n",
       date( ), time( ), field,  repeat_time / 1 ms, int( P1.LENGTH / 1 ns ),
       int( P2.LENGTH / 1 ns ), int( P3.LENGTH / 1 ns ),
       int( p1_to_p2_dist / 1 ns ), int( p1_to_p2_incr / 1 ns ),
       int( p2_to_p3_dist / 1 ns ), N_Avg );

save_comment( File, "%" );
