/* $Id$ */

=== TRIG_MODE menu [ "EXTERNAL", "INTERNAL" ] [ 1 ] "Trigger mode"
=== REP_TIME float [ 0.001 : ] [ 50 ] "Repetition time" "ms"
=== P1_POS int [ 200 : 167772150 ] [ 200 ] "Position of 1st MW pulse" "ns"
=== P1_LEN int [ 10 : 167772150 ] [ 100 ] "Length of 1st MW pulse" "ns"
=== P2_LEN int [ 10 : 167772150 ] [ 200 ] "Length of 2nd MW pulse" "ns"
=== P3_LEN int [ 10 : 167772150 ] [ 200 ] "Length of 3rd MW pulse" "ns"
=== P1_P2_DIST int [ 100 : 167772150 ] [ 400 ] "P1-P2 distance" "ns"
=== P2_P3_DIST int [ 100 : 167772150 ] [ 400 ] "P2-P3 distance" "ns"
=== DET_OFFSET int [ -167772150 : 167772150 ] [ 0 ] "Detection offset" "ns"
=== N_AVG menu [ "1", "3", "10", "30", "100", "300", "1000", \
===              "3000", "10000" ] [ 3 ] "Number of averages"
=== START_FIELD float [ 0 : 139022.24 ] [ 80000 ] "Start field" "G"
=== END_FIELD float [ 0 : 139022.24 ] [ 82000 ] "End field" "G"
=== FIELD_STEP float [ 0.14288 :  ] [ 1 ] "Field step" "G"
=== ADC_CHANNEL menu [ "0", "1" ] "ADC channel to use"
=== SHOW_PREV button [ OFF ] "Show pulse preview"
=== WAIT_TIME float [ 0.0 : ] [ 5.0 ] "Magnet wait time" "s"
DEVICES:

ips120_10;
itc503;
rb8509;
rb_pulser_j;


VARIABLES:

repeat_time   = REP_TIME ms;
p1_pos        = P1_POS ns;
p1_to_p2_dist = P1_P2_DIST ns;
p2_to_p3_dist = P2_P3_DIST ns;
p1_len        = P1_LEN ns;
p2_len        = P2_LEN ns;
p3_len        = P3_LEN ns;
det_offset    = DET_OFFSET ns;

start_field   = START_FIELD G;
end_field     = END_FIELD G;
=== if ( START_FIELD <= END_FIELD )
field_step    = magnet_field_step_size( FIELD_STEP G );
=== else
field_step    = - magnet_field_step_size( FIELD_STEP G );
=== endif

N_Avg    = N_AVG;
N_Points = ceil( ( end_field - start_field ) / field_step ) + 1;
field[ N_Points ];
wait_time = WAIT_TIME s;
I = 0;
J = 0;
L = 0;
K;
data[ *, * ];
avg[ N_Points ] = float_slice( N_Points );
File1, File2;
B1, B2, B3, B4;
start_temp;


ASSIGNMENTS:

TIMEBASE: 10 ns;
=== if ( TRIG_MODE eq "EXTERNAL" )
TRIGGER_MODE: EXTERNAL;
=== else
TRIGGER_MODE: TRIG_MODE, REPEAT_TIME = repeat_time;
=== endif


PREPARATIONS:

P1:  FUNCTION = MICROWAVE,
     START    = p1_pos,
     LENGTH   = p1_len;

P2:  FUNCTION = MICROWAVE,
     START    = P1.START + p1_to_p2_dist + 0.5 * ( P1.LENGTH - p2_len ),
     LENGTH   = p2_len;

P3:  FUNCTION = MICROWAVE,
     START    = P2.START + p2_to_p3_dist + 0.5 * ( P2.LENGTH - p3_len ),
     LENGTH   = p3_len;

P4:  FUNCTION = DETECTION,
     START    = P3.START + det_offset + p1_to_p2_dist + 0.5 * P3.LENGTH,
     LENGTH   = 100 ns;

=== if SHOW_PREV
pulser_show_pulses( );
=== endif

init_1d( 2, N_Points, "Field [G]", "Echo amplitude [a.u.]" );


EXPERIMENT:
=== if ( TRIG_MODE eq "EXTERNAL" )
print( "Please make sure the repetition time is set correctly!\n" );
=== endif

pulser_state( "ON" );
start_temp = temp_contr_temperature( );

field[ 1 ] = magnet_field( start_field );

IF abs( field[ 1 ] - start_field ) > 0.05 * FIELD_STEP G {
   print( "Start field had to be adjusted from # G to # G.\n",
          start_field, field[ 1 ] );
}

IF abs( FIELD_STEP G - abs( field_step ) ) > 0.05 * FIELD_STEP G {
    print( "Field step size had to be adjusted from # G to # G.\n",
           FIELD_STEP G, abs( field_step ) );
}

start_field = field[ 1 ];
end_field = start_field + ( N_Points - 1 ) * field_step;

=== if ( START_FIELD <= END_FIELD )
change_scale( start_field, field_step );
=== else
change_scale( end_field, - field_step );
=== endif

IF wait_time > 1 us {
    wait( wait_time );
}

/* Open the file for averaged data */

File1 = get_file( "", "*.avg", "", "", "avg" );

/* Create the toolbox */

hide_toolbox( "ON" );
B1 = output_create( "INT_OUTPUT", "Current scan" );
B2 = output_create( "FLOAT_OUTPUT", "Current field [G]", "%.3f" );
B3 = output_create( "FLOAT_OUTPUT", "Current temperature [K]", "%.1f" );
B4 = button_create( "PUSH_BUTTON", "Stop after end of scan" );
hide_toolbox( "OFF" );


FOREVER {
    L = 0;
    J += 1;
    output_value( B1, J );                // Update the scan count display

    FOR I = 1 : N_Points {
        output_value( B2, field[ I ] );
        output_value( B3, temp_contr_temperature( ) );
        wait( 1.1 * repeat_time * N_Avg );

=== if ( ADC_CHANNEL eq "0" )
        data[ J, I ] = - daq_get_voltage( CH0 );
=== else
        data[ J, I ] = - daq_get_voltage( CH1 );
=== endif
        avg[ I ] = add_to_average( avg[ I ], data[ J, I ], J );
        L += 1;
=== if ( START_FIELD <= END_FIELD )
        display_1d( I, data[ J, I ], 1,
                    I, avg[ I ],     2 );
=== else
        display_1d( N_Points - I + 1, data[ J, I ], 1,
                    N_Points - I + 1, avg[ I ],     2 );
=== endif

        IF I < N_Points {
            field[ I + 1 ] = magnet_field( field[ I ] + field_step );
        }
    }

    IF button_state( B4 ) {               // Stop on user request
        BREAK;
    }

    field[ 1 ] = magnet_field( start_field );
    IF wait_time > 1 us {
        wait( wait_time );
    }
}

ON_STOP:

/* If L is 0 and J is 1 the first scan hadn't even started */

IF L > 0 OR J > 1 {

    /* If J is greater than 1 but L less than N_Points we got stopped during
       a scan and have to throw away the last, unfinished one */

    IF J > 1 AND L < N_Points {
        L = N_Points;
        J -= 1;
        IF J > 1 {
            avg = float_slice( N_Points );
            FOR K = 1 : J {
                avg = add_to_average( avg, data[ K ], K );
            }
        }
    }

    /* If there was more than one scan write out all single scan data */

    IF J > 1 {
        File2 = clone_file( File1, "avg", "scans" );
=== if ( START_FIELD <= END_FIELD )
        FOR I = 1 : N_Points {
=== else
        FOR I = N_Points : 1 : -1 {
=== endif
            fsave( File2, "#", field[ I ] );

            FOR K = 1 : J {
                fsave( File2, ",#", data[ K, I ] );
            }
            fsave( File2, "\n" );
        }

        fsave( File2,
               "\n"
               "% Date:                   # #\n"
               "% Script:                 3_pulse_epr (J-band, Leiden)\n"
               "% Start field:            # G\n"
               "% End field:              # G\n"
               "% Field step:             # G\n"
               "% Repetition time:        # ms\n"
               "% Length of 1st MW pulse: # ns\n"
               "% Length of 2nd MW pulse: # ns\n"
               "% Length of 3rd MW pulse: # ns\n"
               "% P1-P2 separation:       # ns\n"
               "% P2-P3 separation:       # ns\n"
               "% Number of averages:     >= #\n"
               "% Number of scans:        #\n"
               "% ADC gain:               4\n"
               "% ADC channel:            ADC_CHANNEL\n"
               "% Temperature at start:   # K\n"
               "% Temperature at end:     # K\n",
               date( ), time( ), field[ 1 ], field[ N_Points ], field_step,
               repeat_time / 1 ms, round( P1.LENGTH / 1 ns ),
               round( P2.LENGTH / 1 ns ), round( P3.LENGTH / 1 ns ),
               round( p1_to_p2_dist / 1 ns ), round( p2_to_p3_dist / 1 ns ),
               N_Avg, J, start_temp, temp_contr_temperature( ) );
    }

    /* Write the averaged data into the first file */

=== if ( START_FIELD <= END_FIELD )
    FOR K = 1 : L {
=== else
    FOR K = L : 1 : -1 {
=== endif
        fsave( File1, "#,#\n", field[ K ], avg[ K ] );
    }

    fsave( File1,
           "\n"
           "% Date:                   # #\n"
           "% Script:                 3_pulse_epr (J-band, Leiden)\n"
           "% Start field:            # G\n"
           "% End field:              # G\n"
           "% Field step:             # G\n"
           "% Repetition time:        # ms\n"
           "% Length of 1st MW pulse: # ns\n"
           "% Length of 2nd MW pulse: # ns\n"
           "% Length of 3rd MW pulse: # ns\n"
           "% P1-P2 separation:       # ns\n"
           "% P2-P3 separation:       # ns\n"
           "% Number of averages:     >= #\n"
           "% Number of scans:        #\n"
           "% ADC gain:               4\n"
           "% ADC channel:            ADC_CHANNEL\n"
           "% Temperature at start:   # K\n"
           "% Temperature at end:     # K\n",
           date( ), time( ), field[ 1 ], field[ L ], field_step,
           repeat_time / 1 ms, round( P1.LENGTH / 1 ns ),
           round( P2.LENGTH / 1 ns ), round( P3.LENGTH / 1 ns ),
           round( p1_to_p2_dist / 1 ns ), round( p2_to_p3_dist / 1 ns ),
           N_Avg, J, start_temp, temp_contr_temperature( ) );

    save_comment( File1, "% " );
}
