DEVICES:

lecroy9400;
er032m;


VARIABLES:

start_field[ 3 ] = { 3240 G, 3380 G, 3495 G };
end_field[ 3 ]   = { 3250 G, 3390 G, 3515 G };
field_step = 0.7 G;


field = start_field[ 1 ];

data[ * ];
I = 1;
J;
K;

F1, F2;


PREPARATIONS:

init_2d( 1, 0, 0, 0, 0, 0, 0, "Time axis", "Field [G]" );
magnet_setup( start_field[ 1 ], field_step );
digitizer_averaging( FUNC_E, CH1, 50 );


EXPERIMENT:

change_scale( 0, 10e6 * digitizer_time_per_point( ),
			  start_field[ 1 ], field_step );

F1 = get_file( "Enter data file name:", "*.dat", "", "", "dat" );
F2 = clone_file( F1, "dat", "par" );

save_comment( F2 );
FOR K = 1 : 3 {
	fsave( F2, "Start field[ # ] = # G\n", K, start_field[ K ] );
	fsave( F2, "End field[ # ]   = # G\n", K, end_field[ K ] );
}
fsave( F2, "Field step  = # G\n", field_step );
fsave( F2, "Time scale  = # us\n", digitizer_timebase( ) * 1.0e6 );
save_program( F2 );


FOR K = 1 : 3 {
	set_field( start_field[ K ] );
	field = start_field[ K ];
	WHILE field <= end_field[ K ] {
		  digitizer_start_acquisition( );
		  data = digitizer_get_curve( FUNC_E );
		  FOR J = 1 : size( data, 1 ) {
		  fsave( F1, "# ", data[ J ] );
		  }
		  fsave( F1, "\n" );
		  display( 1, round( ( field - start_field[ 1 ] ) / field_step ) + 1,
				   data );
		  field = sweep_up( );
		  I +=1;
	}
}