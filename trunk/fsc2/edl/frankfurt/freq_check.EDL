=== DT float [ 0.5 : ][ 1 ] "Time step" "s"

DEVICES:

hp5342a;


VARIABLES:

dt = DT s;
File;
t1, t2, t3;
res = 1.0 Hz;
I = 1;
freq;
S;


PREPARATIONS:

init_1d( 1, 0, 0, dt, "Time [s]", "Frequency [GHz]" );


EXPERIMENT:

// Set resolution at which the counter is fast enough

IF dt <= 0.7 s {
    res = 1 kHz;
} ELSE IF dt < 1.0 s {
    res = 100 Hz;
} ELSE IF dt < 1.5 s {
    res = 10 Hz;
}

freq_counter_resolution( res );

File = get_file( );

// Get two dat points to get the frequency counter up to speed

freq_counter_measure( );
freq_counter_measure( );

delta_time( );

S = button_create( "PUSH_BUTTON", "Start/Stop data acquistion" );
button_state( S, 1 );

t1 = 0.0;

FOREVER {
    IF button_state( S ) {
        t1 = delta_time( );
        freq = freq_counter_measure( ) / 1 GHz;
        display_1d( I, freq );
        I += 1;
        t2 = delta_time( );
        IF dt > t2 {
            wait( dt - t2 );
        }
		t3 = delta_time( );
        fsave( File, "# #\n", t1 + t2 + t3, freq );
    } ELSE {
        wait( 0.25 s );
    }
}
