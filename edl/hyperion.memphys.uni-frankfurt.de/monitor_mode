#!/usr/local/bin/fsc2 -S
DEVICES:

fsps25;
sr530;


VARIABLES:

current;
current_step = 25 mA;
max_current;
max_current_step = 1 A;
min_current_step = 1 mA;
Acq_state = 1;
Sweep_state = 0;
data[ 2 ];
I = 1;
new;

Current;
Current_step;
Sweep_up;
Sweep_stop;
Sweep_down;
Clear;
Start_stop;

SWEEP_STOPPED = 0;
SWEEP_UP = 1;
SWEEP_DOWN = -1;


PREPARATIONS:

init_1d( 2, "Point #", "Signal [mV]" );


EXPERIMENT:

current = magnet_field( );
max_current = magnet_max_current( );

hide_toolbox( "ON" );

Current = input_create( "FLOAT_INPUT", current, "Magnet current [A]" );
Current_step = input_create( "FLOAT_INPUT", current_step / 1 mV,
			     "Current step size [mA]" );

Sweep_up   = button_create( "RADIO_BUTTON", "Sweep up" );
Sweep_stop = button_create( "RADIO_BUTTON", Sweep_up, "Stop sweep" );
Sweep_down = button_create( "RADIO_BUTTON", Sweep_up, "Sweep down" );
button_state( Sweep_stop, "ON" );

Clear = button_create( "NORMAL_BUTTON", "Clear Screen" );

Start_stop = button_create( "PUSH_BUTTON", "Aquisition running/stopped" );
button_state( Start_stop, "ON" );

hide_toolbox( "OFF" );

FOREVER {
    IF    (   Acq_state AND toolbox_changed( ) )
	   OR ( ! Acq_state AND toolbox_wait( 1.0e12 s ) ) {
        IF button_changed( Sweep_stop ) {
            button_state( Sweep_stop );
            draw_marker( I, "YELLOW" );
            Sweep_state = SWEEP_STOPPED;
        }

        IF button_changed( Sweep_up ) {
            button_state( Sweep_up );
            draw_marker( I, "RED" );
            Sweep_state = SWEEP_UP;
        }

        IF button_changed( Sweep_down ) {
            button_state( Sweep_down );
            draw_marker( I, "GREEN" );
            Sweep_state = SWEEP_DOWN;
        }

        IF button_state( Sweep_stop ) {
            IF input_changed( Current ) {
                new = input_value( Current );
                IF abs( new ) <= max_current {
                    object_enable( Sweep_up, "OFF" );
                    object_enable( Sweep_down, "OFF" );
                    current = magnet_field( new );
                    object_enable( Sweep_up, "ON" );
                    object_enable( Sweep_down, "ON" );
                }   
                input_value( Current, current );
            }

            IF input_changed( Current_step ) {
                new = input_value( Current_step ) * 1 mA;
                IF new >= min_current_step AND
                   new <= max_current_step {
                    current_step =   min_current_step
		                   * round( new / min_current_step );
                }
                input_value( Current_step, current_step / 1 mA );
            }
        }

        IF button_state( Clear ) {
            clear_curve( );
            clear_marker( );
            I = 1;
            rescale( 64 );
        }

        IF button_changed( Start_stop ) {
            Acq_state = button_state( Start_stop );
			IF ! Acq_state {
				NEXT;
			}
        }
    }

    IF Sweep_state == SWEEP_UP {
        IF abs( current + current_step ) <= max_current {
            current = magnet_field( current + current_step );
            input_value( Current, current );
        } ELSE {
            button_state( Sweep_stop, "ON" );
            Sweep_state = SWEEP_STOPPED;
        }
    }

    IF Sweep_state == SWEEP_DOWN {
        IF abs( current - current_step ) <= max_current {
            current = magnet_field( current - current_step );
            input_value( Current, current );
        } ELSE {
            button_state( Sweep_stop, "ON" );
            Sweep_state = SWEEP_STOPPED;
        }
    }

    IF Acq_state {
        wait( lockin_time_constant( ) );
	data = lockin_get_data( 1, 2 ) / 1 mV;
        display_1d( I, data[ 1 ], 1,
		    I, data[ 2 ], 2 );
        I += 1;
    }
}
